PREFIX spatialf:  <http://jena.apache.org/function/spatial#>
PREFIX piirrite:  <http://piirrite.univ-lyon1.fr/ontology/core#>
PREFIX piirritev: <http://piirrite.univ-lyon1.fr/vocabulary#>
PREFIX geo:       <http://www.opengis.net/ont/geosparql#>
PREFIX uom:       <http://www.opengis.net/def/uom/OGC/1.0/>
PREFIX saref:     <https://saref.etsi.org/core/>

# CQ3 Where is the vending machine that is nearest to a bicycle parking lot that has a capacity of at least 20?

SELECT ?parking ?coffeeMachine ?distance WHERE {
  ?parking a piirrite:SpatialPoint ;
    geo:hasGeometry ?parkingGeom ;
    saref:hasProperty ?parkingProp .

  ?parkingGeom a geo:Geometry ;
    geo:asWKT ?parkingWKT .

  ?parkingProp a piirritev:AmenityBicycleParking ;
    piirrite:hasAmenityBicycleParkingCapacity ?capacity .

  FILTER(?capacity >= 20)

  ?coffeeMachine a piirrite:SpatialPoint ;
    geo:hasGeometry ?distGeom ;
    saref:hasProperty ?distProp .

  ?distGeom a geo:Geometry ;
    geo:asWKT ?distWKT .

  ?distProp a piirritev:AmenityVendingMachine .

  BIND(spatialf:distance(?distWKT, ?parkingWKT, uom:metre) AS ?distance)
}
ORDER BY ASC(?distance)
LIMIT 1