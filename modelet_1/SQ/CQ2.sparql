PREFIX spatialf:  <http://jena.apache.org/function/spatial#>
PREFIX piirrite:  <http://piirrite.univ-lyon1.fr/ontology/core#>
PREFIX piirritev: <http://piirrite.univ-lyon1.fr/vocabulary#>
PREFIX geo:       <http://www.opengis.net/ont/geosparql#>
PREFIX uom:       <http://www.opengis.net/def/uom/OGC/1.0/>
PREFIX saref:     <https://saref.etsi.org/core/>

# CQ2 Is there a tramway station within 100 meters of a given point?

ASK
WHERE {
  BIND("POINT(4.882 45.787)"^^geo:wktLiteral AS ?refPoint)

  ?pt a piirrite:SpatialPoint ;
      geo:hasGeometry ?geom ;
      saref:hasProperty ?prop .

  ?geom a geo:Geometry ;
        geo:asWKT ?wkt .

  ?prop a piirritev:OsmRailwayTramStop .

  FILTER( spatialf:distance(?wkt, ?refPoint, uom:metre) <= 100 )
}