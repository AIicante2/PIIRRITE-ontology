PREFIX spatialf: <http://jena.apache.org/function/spatial#>
PREFIX incit:    <http://incit.univ-lyon1.fr/ontology/core#>
PREFIX incitv:   <http://incit.univ-lyon1.fr/vocabulary#>
PREFIX geo:      <http://www.opengis.net/ont/geosparql#>
PREFIX uom:      <http://www.opengis.net/def/uom/OGC/1.0/>
PREFIX saref:    <https://saref.etsi.org/core/>

# CQ1-2
# What is the nearest coffee machine to a given point?

SELECT ?pt ?distance WHERE {
  BIND("POINT(4.87 45.78)"^^geo:wktLiteral AS ?refPoint)

  ?pt a incit:SpatialPoint ;
      geo:hasGeometry ?geom ;
      saref:hasProperty ?prop .

  ?geom a geo:Geometry ;
        geo:asWKT ?wkt .

  ?prop a incitv:OsmAmenityVendingMachine .

  BIND(spatialf:distance(?wkt, ?refPoint, uom:metre) AS ?distance)
}
ORDER BY ASC(?distance)
LIMIT 1
