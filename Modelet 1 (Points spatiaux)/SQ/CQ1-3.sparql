PREFIX spatialf: <http://jena.apache.org/function/spatial#>
PREFIX incit:    <http://incit.univ-lyon1.fr/ontology/core#>
PREFIX incitv:   <http://incit.univ-lyon1.fr/vocabulary#>
PREFIX geo:      <http://www.opengis.net/ont/geosparql#>
PREFIX uom:      <http://www.opengis.net/def/uom/OGC/1.0/>
PREFIX saref:    <https://saref.etsi.org/core/>

# CQ1-3
# Where is the nearest coffee machine to a bicycle parking lot that has a capacity of at least 20?

SELECT ?parking ?distributeur ?distance WHERE {
  ?parking a incit:SpatialPoint ;
           geo:hasGeometry ?parkingGeom ;
           saref:hasProperty ?parkingProp .

  ?parkingGeom a geo:Geometry ;
               geo:asWKT ?parkingWKT .

  ?parkingProp a incitv:OsmAmenityBicycleParking ;
               incit:hasOsmBicycleParkingCapacity ?capacity .

  FILTER(?capacity > 20)

  ?distributeur a incit:SpatialPoint ;
                geo:hasGeometry ?distGeom ;
                saref:hasProperty ?distProp .

  ?distGeom a geo:Geometry ;
            geo:asWKT ?distWKT .

  ?distProp a incitv:OsmAmenityVendingMachine .

  BIND(spatialf:distance(?distWKT, ?parkingWKT, uom:metre) AS ?distance)
}
ORDER BY ASC(?distance)
LIMIT 1