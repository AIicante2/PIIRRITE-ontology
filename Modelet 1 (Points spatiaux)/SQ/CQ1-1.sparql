PREFIX spatialf: <http://jena.apache.org/function/spatial#>
PREFIX incit:    <http://incit.univ-lyon1.fr/ontology/core#>
PREFIX incitv:   <http://incit.univ-lyon1.fr/vocabulary#>
PREFIX geo:      <http://www.opengis.net/ont/geosparql#>
PREFIX uom:      <http://www.opengis.net/def/uom/OGC/1.0/>
PREFIX saref:    <https://saref.etsi.org/core/>

# CQ1-1
# Is there a coffee machine within 100 meters of a given point?
ASK {
  BIND("POINT(4.87 45.78)"^^geo:wktLiteral AS ?refPoint)

  ?pt a incit:SpatialPoint ;
      geo:hasGeometry ?geom ;
      saref:hasProperty ?prop .

  ?geom a geo:Geometry ;
        geo:asWKT ?wkt .

  ?prop a incitv:OsmAmenityVendingMachine .

  FILTER( spatialf:distance(?wkt, ?refPoint, uom:metre) <= 100 )
}