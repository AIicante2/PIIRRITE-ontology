PREFIX geof:      <http://www.opengis.net/def/function/geosparql/>
PREFIX piirrite:  <http://piirrite.univ-lyon1.fr/ontology/core#>
PREFIX piirritev: <http://piirrite.univ-lyon1.fr/vocabulary#>
PREFIX geo:       <http://www.opengis.net/ont/geosparql#>
PREFIX saref:     <https://saref.etsi.org/core/>
PREFIX xsd:       <http://www.w3.org/2001/XMLSchema#>

# CQ5 Is there a coffee machine on the first floor of a given building?

ASK
WHERE {
  ?building a piirrite:TopologicalSegment ;
    saref:hasProperty ?buildingProp ;
    geo:hasGeometry ?buildingGeom .
  
  ?buildingProp a piirritev:BuildingUniversity ;
    piirrite:hasBuildingUniversityName "Quai 43"^^xsd:string .
  
  ?buildingGeom geo:asWKT ?buildingWKT .
  
  ?point a piirrite:SpatialPoint ;
    geo:hasGeometry ?pointGeom ;
    saref:hasProperty ?vendingMachineProp ;
    saref:hasProperty ?levelProp .
  
  ?vendingMachineProp a piirritev:AmenityVendingMachine .
  
  ?levelProp a piirritev:Level ;
    saref:hasValue 1 .
  
  ?pointGeom geo:asWKT ?pointWKT .
  
  FILTER( geof:sfWithin(?pointWKT, ?buildingWKT) )
}